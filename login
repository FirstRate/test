-* This program sets-up the dialog pop-up for the ACE screen
-* in Advisor
-* Programmer: JKF

-IF &&ALLACCTS EQ 'Y' THEN GOTO S_SP JOIN
JOIN ACCT IN FRPUSAC2 TO ACCT IN FRPAIR AS SJ
TABLE FILE FRPUSAC2
    PRINT *
    ON TABLE HOLD AS SECPORTS
END
JOIN CLEAR *
-RUN
-S_SP

-SET &BaseTable = IF &&ALLACCTS EQ 'Y' THEN 'FRPAIR' ELSE 'SECPORTS';

JOIN LEFT_OUTER FILE &BaseTable AT ACTIVE TAG AIR
    TO UNIQUE
    FILE FRPFLVAL AT VALUE TAG VAL
    WHERE AIR.ACTIVE EQ VAL.VALUE;
    WHERE VAL.FILTER EQ 'ACTIVE';
END
TABLE FILE &BaseTable
    SUM
        COMPUTE ActiveValue/A3 = IF ACTIVE EQ MISSING THEN '.' ELSE ACTIVE; NOPRINT
        COMPUTE ActiveDesc/A50 = IF VAL_DESC EQ MISSING THEN 'MISSING (' || ActiveValue  || ')' ELSE VAL_DESC; NOPRINT
        COMPUTE ActiveOpt/A79 = '<option value="' || ActiveValue || '">' || ActiveDesc || '</option>';
    BY ACTIVE NOPRINT
    WHERE ACCT LIKE 'AGG%'
    ON TABLE SAVE AS ACTIVEDD FORMAT ALPHA
END
JOIN CLEAR *
-RUN


DEFINE FILE FRPRPTS
    RPT_IDA/A9 = TRIM('L', EDIT(RPT_ID), 9, '0', 1, 'A9');
    Option/A85 = '<option value="' || RPT_IDA || '">' || RPT_NAME || '</option>';
END
TABLE FILE FRPRPTS
    SUM Option
    BY RPT_NAME NOPRINT
    WHERE SCREEN EQ 'NONE'
    WHERE TYP EQ 'ACE'
    ON TABLE SAVE AS QUERIES FORMAT ALPHA
END
DEFINE FILE FRPRPTS CLEAR
END
-RUN

-HTMLFORM aceui.html
